// SPDX-License-Identifier: MIT
pragma solidity ^0.8.30;

import {DeployRiscZeroContracts} from "@anoma-evm-pa-testing/script/DeployRiscZeroContracts.s.sol";
import {IForwarder} from "@anoma-evm-pa/interfaces/IForwarder.sol";
import {ProtocolAdapter} from "@anoma-evm-pa/ProtocolAdapter.sol";

import {Time} from "@openzeppelin-contracts/utils/types/Time.sol";
import {IPermit2, ISignatureTransfer} from "@permit2/src/interfaces/IPermit2.sol";
import {RiscZeroGroth16Verifier} from "@risc0-ethereum/groth16/RiscZeroGroth16Verifier.sol";
import {RiscZeroVerifierRouter} from "@risc0-ethereum/RiscZeroVerifierRouter.sol";

import {Test, Vm, stdError} from "forge-std/Test.sol";

import {ERC20Forwarder} from "../src/ERC20Forwarder.sol";
import {ERC20ForwarderPermit2} from "../src/ERC20ForwarderPermit2.sol";

import {ERC20Example, ERC20WithFeeExample} from "../test/examples/ERC20.e.sol";
import {Permit2Signature} from "./libs/Permit2Signature.sol";
import {DeployPermit2} from "./script/DeployPermit2.s.sol";

contract ERC20ForwarderTest is Test {
    using ERC20ForwarderPermit2 for ERC20ForwarderPermit2.Witness;
    using Permit2Signature for Vm;

    address internal constant _EMERGENCY_COMMITTEE = address(uint160(1));
    uint128 internal constant _TRANSFER_AMOUNT = 1000;
    bytes internal constant _EXPECTED_OUTPUT = "";
    bytes32 internal constant _ACTION_TREE_ROOT = bytes32(uint256(0));

    bytes32 internal _logicRef;

    address internal _alice;
    uint256 internal _alicePrivateKey;

    ProtocolAdapter internal _pa;
    IForwarder internal _fwd;
    IPermit2 internal _permit2;
    ERC20Example internal _erc20;
    ERC20WithFeeExample internal _erc20FeeAdd;
    ERC20WithFeeExample internal _erc20FeeSub;

    ISignatureTransfer.PermitTransferFrom internal _defaultPermit;
    bytes32 internal _defaultPermitSigR;
    bytes32 internal _defaultPermitSigS;
    uint8 internal _defaultPermitSigV;
    bytes internal _defaultWrapInput;
    bytes internal _defaultUnwrapInput;

    // Copied since we can't `import {SignatureExpired, InvalidNonce} from "@permit2/src/PermitErrors.sol";`
    // because of the incompatible solc pragma.
    error SignatureExpired(uint256 signatureDeadline);
    error InvalidNonce();

    function setUp() public virtual {
        _logicRef = bytes32(uint256(1));

        _alicePrivateKey = 0xc522337787f3037e9d0dcba4dc4c0e3d4eb7b1c65598d51c425574e8ce64d140;
        _alice = vm.addr(_alicePrivateKey);

        // Deploy token and mint for alice
        _erc20 = new ERC20Example();
        _erc20FeeAdd = new ERC20WithFeeExample({isFeeAdded: true});
        _erc20FeeSub = new ERC20WithFeeExample({isFeeAdded: false});

        // Get the Permit2 contract
        _permit2 = _permit2Contract();

        // Deploy RISC Zero contracts
        (RiscZeroVerifierRouter router,, RiscZeroGroth16Verifier verifier) =
            new DeployRiscZeroContracts().run({admin: msg.sender, guardian: msg.sender});

        // Deploy the protocol adapter
        _pa = new ProtocolAdapter(router, verifier.SELECTOR(), _EMERGENCY_COMMITTEE);

        // Deploy the ERC20 forwarder
        _fwd = new ERC20Forwarder({
            protocolAdapter: address(_pa), emergencyCommittee: _EMERGENCY_COMMITTEE, logicRef: _logicRef
        });

        _defaultPermit = ISignatureTransfer.PermitTransferFrom({
            permitted: ISignatureTransfer.TokenPermissions({token: address(_erc20), amount: _TRANSFER_AMOUNT}),
            nonce: 123,
            deadline: Time.timestamp() + 5 minutes
        });

        (_defaultPermitSigR, _defaultPermitSigS, _defaultPermitSigV) = vm.permitWitnessTransferFromSignature({
            domainSeparator: _permit2.DOMAIN_SEPARATOR(),
            permit: _defaultPermit,
            privateKey: _alicePrivateKey,
            spender: address(_fwd),
            witness: ERC20ForwarderPermit2.Witness(_ACTION_TREE_ROOT).hash()
        });

        _defaultWrapInput = abi.encode(
            /*       callType */
            ERC20Forwarder.CallType.Wrap,
            /*          token */
            _defaultPermit.permitted.token,
            /*         amount */
            _defaultPermit.permitted.amount,
            /*      wrap data */
            ERC20Forwarder.WrapData({
                nonce: _defaultPermit.nonce,
                deadline: _defaultPermit.deadline,
                owner: _alice,
                actionTreeRoot: _ACTION_TREE_ROOT,
                r: _defaultPermitSigR,
                s: _defaultPermitSigS,
                v: _defaultPermitSigV
            })
        );

        _defaultUnwrapInput = abi.encode( /* callType */
            ERC20Forwarder.CallType.Unwrap,
            /*       token */
            address(_erc20),
            /*      amount */
            _TRANSFER_AMOUNT,
            /* unwrap data */
            ERC20Forwarder.UnwrapData({receiver: _alice})
        );
    }

    function testFuzz_enum_panics(uint8 v) public {
        uint8 callTypeEnumLength = uint8(type(ERC20Forwarder.CallType).max) + 1;

        if (v < callTypeEnumLength) {
            ERC20Forwarder.CallType(v);
        } else {
            vm.expectRevert(stdError.enumConversionError);
            ERC20Forwarder.CallType(v);
        }
    }

    function test_forwardCall_reverts_on_invalid_calltype() public {
        vm.prank(address(_pa));
        vm.expectRevert();
        _fwd.forwardCall({logicRef: _logicRef, input: abi.encode(type(uint8).max)});
    }

    function test_unwrap_sends_funds_to_the_user() public {
        _erc20.mint({to: address(_fwd), value: _TRANSFER_AMOUNT});
        uint256 startBalanceAlice = _erc20.balanceOf(_alice);
        uint256 startBalanceForwarder = _erc20.balanceOf(address(_fwd));

        vm.prank(address(_pa));
        bytes memory output = _fwd.forwardCall({logicRef: _logicRef, input: _defaultUnwrapInput});

        assertEq(keccak256(output), keccak256(_EXPECTED_OUTPUT));
        assertEq(_erc20.balanceOf(_alice), startBalanceAlice + _TRANSFER_AMOUNT);
        assertEq(_erc20.balanceOf(address(_fwd)), startBalanceForwarder - _TRANSFER_AMOUNT);
    }

    function test_unwrap_reverts_if_the_withdrawn_amount_is_not_the_unwrap_amount() public {
        uint256 fee = _erc20FeeAdd.FEE();

        _erc20FeeAdd.mint({to: address(_fwd), value: _TRANSFER_AMOUNT + fee});

        bytes memory input = abi.encode(
            /*    callType */
            ERC20Forwarder.CallType.Unwrap,
            /*       token */
            address(_erc20FeeAdd),
            /*      amount */
            _TRANSFER_AMOUNT,
            /* unwrap data */
            ERC20Forwarder.UnwrapData({receiver: _alice})
        );

        uint256 actualWithdrawalAmount = _TRANSFER_AMOUNT + fee;

        vm.prank(address(_pa));
        vm.expectRevert(
            abi.encodeWithSelector(ERC20Forwarder.BalanceMismatch.selector, _TRANSFER_AMOUNT, actualWithdrawalAmount),
            address(_fwd)
        );
        _fwd.forwardCall({logicRef: _logicRef, input: input});
    }

    function test_unwrap_does_not_revert_if_the_amount_is_zero() public {
        _erc20.mint({to: address(_fwd), value: _TRANSFER_AMOUNT});
        uint256 startBalanceAlice = _erc20.balanceOf(_alice);
        uint256 startBalanceForwarder = _erc20.balanceOf(address(_fwd));

        bytes memory unwrapInputWithZeroAmount = abi.encode( /* callType */
            ERC20Forwarder.CallType.Unwrap,
            /*    token */
            address(_erc20),
            /*   amount */
            0,
            /*       to */
            ERC20Forwarder.UnwrapData({receiver: _alice})
        );

        vm.prank(address(_pa));
        bytes memory output = _fwd.forwardCall({logicRef: _logicRef, input: unwrapInputWithZeroAmount});

        assertEq(keccak256(output), keccak256(_EXPECTED_OUTPUT));
        assertEq(_erc20.balanceOf(_alice), startBalanceAlice);
        assertEq(_erc20.balanceOf(address(_fwd)), startBalanceForwarder);
    }

    function test_unwrap_emits_the_Unwrapped_event() public {
        _erc20.mint({to: address(_fwd), value: _TRANSFER_AMOUNT});

        vm.prank(address(_pa));
        vm.expectEmit(address(_fwd));
        emit ERC20Forwarder.Unwrapped({token: address(_erc20), to: _alice, amount: _TRANSFER_AMOUNT});

        _fwd.forwardCall({logicRef: _logicRef, input: _defaultUnwrapInput});
    }

    function test_wrap_reverts_if_user_did_not_approve_permit2() public {
        _erc20.mint({to: _alice, value: _TRANSFER_AMOUNT});

        vm.prank(address(_pa));
        vm.expectRevert("TRANSFER_FROM_FAILED", address(_erc20));
        _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});
    }

    function test_wrap_reverts_if_the_signature_expired() public {
        _erc20.mint({to: _alice, value: _TRANSFER_AMOUNT});
        vm.prank(_alice);
        _erc20.approve(address(_permit2), type(uint256).max);

        // Advance time after the deadline
        vm.warp(_defaultPermit.deadline + 1);

        vm.prank(address(_pa));
        vm.expectRevert(abi.encodeWithSelector(SignatureExpired.selector, _defaultPermit.deadline), address(_permit2));
        _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});
    }

    function test_wrap_reverts_if_the_signature_was_already_used() public {
        _erc20.mint({to: _alice, value: 2 * _TRANSFER_AMOUNT});
        vm.prank(_alice);
        _erc20.approve(address(_permit2), type(uint256).max);

        // Use the signature.
        vm.startPrank(address(_pa));
        _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});

        // Reuse the signature.
        vm.expectRevert(abi.encodeWithSelector(InvalidNonce.selector), address(_permit2));
        _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});
    }

    function test_wrap_reverts_if_the_deposited_amount_is_not_the_wrap_amount() public {
        _erc20FeeSub.mint({to: _alice, value: _TRANSFER_AMOUNT});
        vm.prank(_alice);
        _erc20FeeSub.approve(address(_permit2), type(uint256).max);

        ISignatureTransfer.PermitTransferFrom memory permit = ISignatureTransfer.PermitTransferFrom({
            permitted: ISignatureTransfer.TokenPermissions({token: address(_erc20FeeSub), amount: _TRANSFER_AMOUNT}),
            nonce: 123,
            deadline: Time.timestamp() + 30 minutes
        });

        (bytes32 r, bytes32 s, uint8 v) = vm.permitWitnessTransferFromSignature({
            domainSeparator: _permit2.DOMAIN_SEPARATOR(),
            permit: permit,
            privateKey: _alicePrivateKey,
            spender: address(_fwd),
            witness: ERC20ForwarderPermit2.Witness(_ACTION_TREE_ROOT).hash()
        });

        bytes memory input = abi.encode( /*       callType */
            ERC20Forwarder.CallType.Wrap,
            /*          token */
            permit.permitted.token,
            /*         amount */
            permit.permitted.amount,
            /*          nonce */
            permit.nonce,
            /*       deadline */
            permit.deadline,
            /*           from */
            _alice,
            /* actionTreeRoot */
            _ACTION_TREE_ROOT,
            /*      signature */
            r,
            s,
            v
        );

        uint256 actualDepositAmount = _TRANSFER_AMOUNT - _erc20FeeSub.FEE();

        vm.prank(address(_pa));
        vm.expectRevert(
            abi.encodeWithSelector(ERC20Forwarder.BalanceMismatch.selector, _TRANSFER_AMOUNT, actualDepositAmount),
            address(_fwd)
        );
        _fwd.forwardCall({logicRef: _logicRef, input: input});
    }

    function test_wrap_pulls_funds_from_user() public {
        _erc20.mint({to: _alice, value: _TRANSFER_AMOUNT});
        uint256 startBalanceAlice = _erc20.balanceOf(_alice);
        uint256 startBalanceForwarder = _erc20.balanceOf(address(_fwd));

        vm.prank(_alice);
        _erc20.approve(address(_permit2), type(uint256).max);

        vm.prank(address(_pa));
        bytes memory output = _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});

        assertEq(keccak256(output), keccak256(_EXPECTED_OUTPUT));
        assertEq(_erc20.balanceOf(_alice), startBalanceAlice - _TRANSFER_AMOUNT);
        assertEq(_erc20.balanceOf(address(_fwd)), startBalanceForwarder + _TRANSFER_AMOUNT);
    }

    function test_wrap_does_not_revert_if_the_amount_is_zero() public {
        _erc20.mint({to: _alice, value: _TRANSFER_AMOUNT});
        uint256 startBalanceAlice = _erc20.balanceOf(_alice);
        uint256 startBalanceForwarder = _erc20.balanceOf(address(_fwd));

        vm.prank(_alice);
        _erc20.approve(address(_permit2), type(uint256).max);

        ISignatureTransfer.PermitTransferFrom memory permitWithZeroAmount = ISignatureTransfer.PermitTransferFrom({
            permitted: ISignatureTransfer.TokenPermissions({token: address(_erc20), amount: 0}),
            nonce: 123,
            deadline: Time.timestamp() + 5 minutes
        });

        (bytes32 r, bytes32 s, uint8 v) = vm.permitWitnessTransferFromSignature({
            domainSeparator: _permit2.DOMAIN_SEPARATOR(),
            permit: permitWithZeroAmount,
            privateKey: _alicePrivateKey,
            spender: address(_fwd),
            witness: ERC20ForwarderPermit2.Witness(_ACTION_TREE_ROOT).hash()
        });

        bytes memory wrapInputWithZeroAmount = abi.encode( /*       callType */
            ERC20Forwarder.CallType.Wrap,
            /*          token */
            permitWithZeroAmount.permitted.token,
            /*         amount */
            permitWithZeroAmount.permitted.amount,
            /*          nonce */
            permitWithZeroAmount.nonce,
            /*       deadline */
            permitWithZeroAmount.deadline,
            /*           from */
            _alice,
            /* actionTreeRoot */
            _ACTION_TREE_ROOT,
            /*      signature */
            r,
            s,
            v
        );

        vm.prank(address(_pa));
        bytes memory output = _fwd.forwardCall({logicRef: _logicRef, input: wrapInputWithZeroAmount});

        assertEq(keccak256(output), keccak256(_EXPECTED_OUTPUT));
        assertEq(_erc20.balanceOf(_alice), startBalanceAlice);
        assertEq(_erc20.balanceOf(address(_fwd)), startBalanceForwarder);
    }

    function test_wrap_emits_the_Wrapped_event() public {
        _erc20.mint({to: _alice, value: _TRANSFER_AMOUNT});

        vm.prank(_alice);
        _erc20.approve(address(_permit2), type(uint256).max);

        vm.prank(address(_pa));
        vm.expectEmit(address(_fwd));
        emit ERC20Forwarder.Wrapped({token: address(_erc20), from: _alice, amount: _TRANSFER_AMOUNT});
        _fwd.forwardCall({logicRef: _logicRef, input: _defaultWrapInput});
    }

    function test_witness_typeHash_complies_with_eip712() public pure {
        assertEq(ERC20ForwarderPermit2._WITNESS_TYPEHASH, vm.eip712HashType(ERC20ForwarderPermit2._WITNESS_TYPE_DEF));
    }

    function test_witness_structHash_complies_with_eip712() public pure {
        ERC20ForwarderPermit2.Witness memory witness = ERC20ForwarderPermit2.Witness({actionTreeRoot: bytes32(0)});
        assertEq(witness.hash(), vm.eip712HashStruct(ERC20ForwarderPermit2._WITNESS_TYPE_DEF, abi.encode(witness)));
    }

    function _permit2Contract() internal virtual returns (IPermit2 permit2) {
        permit2 = new DeployPermit2().run();
    }
}
